%{
	#include "auxstruct.h"
	#include "htmlpage.h"
	#include "linkedlist/linkedlist.h"
	LinkedList lpags;
	Pagina pag;
%}

%option noyywrap

%x PAGE TITLE REVISION DATE AUTHOR LINKINT LINKEXT SECTION1 SECTION2 SECTION3 SECTION4

%%
"<page>" {
	BEGIN PAGE; 	pag = inicializaPagina();
}

<PAGE>{
	"<title>" 		BEGIN TITLE;
	"<revision>"	BEGIN REVISION;
	.|[\n\t]		;
	"</page>"		criaFicheiroHTML(pag); insereTituloIndice(lpags,pag->titulo); BEGIN INITIAL;
}

<TITLE>{
	.*:.*			BEGIN INITIAL;
	[^<]*			insereTitulo(pag,yytext); printf("%s\n", yytext);
	"</title>" 		BEGIN PAGE;
}

<REVISION>{
	.|[\n\t]		;
	"<timestamp>"	BEGIN DATE;
	"<username>"	BEGIN AUTHOR;
	\=\=			BEGIN SECTION1;
	\=\=\=			BEGIN SECTION2;
	\=\=\=\=		BEGIN SECTION3;
	\=\=\=\=\=		BEGIN SECTION4;
	\[\[[^:\|\]]+/\]\]	insereLinkInt(pag,yytext+2);
	\[\[[^:\|\]]+/\|	insereLinkInt(pag,yytext+2); 
	"http://"		BEGIN LINKEXT;
	"</revision>"	BEGIN PAGE;
}

<DATE>{
	[^<]*			insereData(pag,yytext);
	"</timestamp>" 	BEGIN REVISION;
}

<AUTHOR>{
	[^<]*			insereAutor(pag,yytext);
	"</username>"	BEGIN REVISION; 
}

<SECTION1>{
	.|[\n\t]		BEGIN REVISION;
	[^=\n]*			insereSeccao(0,pag,yytext); 
	\=\= 			BEGIN REVISION; 
}

<SECTION2>{
	[^=\&]+			insereSeccao(1,pag,yytext);
	.|[\n\t]		;
	=\=\= 			BEGIN REVISION; 
}

<SECTION3>{
	[^=]*			insereSeccao(2,pag,yytext); 
	=\=\=\= 		BEGIN REVISION; 
}

<SECTION4>{
	[^=]*			insereSeccao(3,pag,yytext);
	=\=\=\=\= 		BEGIN REVISION; 
}

<LINKEXT>{
	[a-zA-Z0-9/\.\-\_\?\=\&\;\+\-\%]+	insereLinkExt(pag,yytext); BEGIN REVISION; 
}

.|[\n\t]			{ ; }

<<EOF>> 			{ return 0; }

%%
int main(int argc, char *argv[]) {
	if(argc == 1) {
		printf("Necessário passar como argumento página/s XML a analisar!");
	} else {
		int i;
		system("mkdir -p pages; cp -R html/ pages");

		for(i=1; i<argc; i++) {
			yyin = fopen(argv[i],"r");

			if(yyin) {
				lpags = iniciaIndiceTitulo();
				yylex();
				fclose(yyin);
			} else {
				printf("Não foi possível ler o ficheiro %s seleccionado!\n", argv[i]);
			}
		}
		if(lpags)
			criaIndexHTML(lpags);
	}
	return 0;
}